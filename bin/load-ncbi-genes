#!/usr/bin/env perl
use 5.010;
use strict;
use warnings;
use utf8;
use open qw< :std :encoding(UTF-8) >;
use Text::CSV;
use ISDB::Schema;

my $tsv = Text::CSV->new({
    binary   => 1,
    sep_char => "\t",
}) or die "Can't create new Text::CSV: " . Text::CSV->error_diag . "\n";

my $db   = ISDB::Schema->connect_default;
my $txn  = $db->txn_scope_guard;
my $gene = $db->resultset("NCBIGene");

my @header;
my $loaded = 0;
say "Loading genes…";

while (<>) {
    if (/^#/) {
        if ($. == 1) {
            s/^#Format:\s*//;   # stupid prefix
            s/\s+\(.+?\)$//;    # stupid suffix
            @header = split ' ';
        }
        next;
    }

    $tsv->parse($_)
        or die sprintf "Parse error: %s\nInput: «%s»\nLine: $.\n",
            scalar $tsv->error_diag, $tsv->error_input;

    my %row;
    @row{@header} = $tsv->fields;

    $gene->create({
        ncbi_gene_id => $row{GeneID},
        name         => $row{Symbol},
    });

    $loaded++;
    say sprintf "  …%d", $loaded
        if $loaded % 10_000 == 0;
}

$txn->commit;
say "Total: $loaded genes";
