# This is a recs <https://recs.pl> snippet for use with `recs xform`.
# vim: set ft=perl :
use strict;
use warnings;
use 5.010;
our $line;  # from recs

my $t = {
    source_name => "NCI-RID",
    sample => {
        original_id => $r->{origin_id},
        pubmed_id   => $r->{pubmed_id},
    },
    info => {
        source_line => $line + 1,   # header isn't counted
    },
};

# Named captures get copied into the sample document.
my %sample_extractor = (
    25011556 => qr/^(?<subject>[BLR]1)_/i,                                  # Wagner, et al.
    24968937 => qr/^(?<subject>PT[1-5])(?:_Y(?<years_on_art>[\d.]+))?/i,    # Maldarelli, et al.
    12843741 => qr/^(?<clone>(?:AL|AD|C)-\d+)_/,                            # Mack, et al.
    15163705 => qr/^(?<subject>\d+)/,                                       # Han, et al.
    17262715 => qr/^(?<genbank>.+?)(?:\.\d+)?$/,                            # Ikeda, et al.
);
if (my $re = $sample_extractor{ $t->{sample}{pubmed_id} }) {
    if ($t->{sample}{original_id} =~ $re) {
        $t->{sample}{$_} = $+{$_}
            for keys %+;
    }
}

# 23953889, Sherrill-Mix, et al.
$t->{sample}{tissue} = "cell culture"
    if $t->{sample}{pubmed_id} == 23953889;

# 12843741, Mack, et al.
if ($t->{sample}{pubmed_id} == 12843741 and $t->{sample}{clone}) {
    my %clones;

    # Subjects
    $clones{$_}{subject} = "Case #1" for map { "AL-$_" }  1 .. 12;
    $clones{$_}{subject} = "Case #2" for map { "AL-$_" } 13 .. 16;
    $clones{$_}{subject} = "Case #3" for map { "AL-$_" } 17 .. 19;
    $clones{$_}{subject} = "Case #4" for map { "AD-$_" }  1 .. 15;
    $clones{$_}{subject} = "Control" for map { "C-$_"  }  1 .. 3;

    # Tissues
    $clones{$_}{tissue} = "lymph node"     for map { "AL-$_" }  1 .. 4, 13 .. 19;
    $clones{$_}{tissue} = "spleen"         for map { "AL-$_" }  5 .. 12;
    $clones{$_}{tissue} = "frontal cortex" for map { "AD-$_" }  1 .. 10;
    $clones{$_}{tissue} = "meninges"       for map { "AD-$_" } 11 .. 13;
    $clones{"AD-14"}{tissue} = "subcortex";
    $clones{"AD-15"}{tissue} = "macrophage";

    for (map { "C-$_" } 1 .. 3) {
        $clones{$_}{tissue}     = "OM-10 cell line";
        $clones{$_}{tissue_url} = "https://www.aidsreagent.org/pdfs/ds1319_011.pdf";
    }

    # Disease
    $clones{$_}{disease} = "angioimmunoblastic lymphadenopathy (AILD)"
        for map { "AL-$_" }  1 .. 12;
    $clones{$_}{disease} = "lymphoma"
        for map { "AL-$_" } 13 .. 19;
    $clones{$_}{disease} = "AIDS dementia"
        for map { "AD-$_" }  1 .. 15;

    # Copy properties on our IS record
    if (my $clone = $clones{ $t->{sample}{clone} }) {
        $t->{sample}{$_} = $clone->{$_}
            for keys %$clone;
    }
}

$t->{sample}{subject} = "$t->{sample}{pubmed_id}/$t->{sample}{subject}"
    if $t->{sample}{subject};

$t->{ltr} = { "5LTR" => "5p", "3LTR" => "3p" }->{ $r->{LTR} }
    if $r->{LTR};

$t->{chromosome} = uc $r->{chr} =~ s/^chr//ir;
$t->{location}   = $r->{insert_position};

delete $t->{chromosome}
    if $t->{chromosome} eq "UN";

delete $t->{location}
    if not $t->{chromosome};

# Convert to zero-origin, interbase coordinates assuming that the reported
# location is base-numbered and indicates the first chromosomal base outside of
# the indicated HIV LTR.  If the HIV LTR is not indicated, assume 5' and hence
# no conversion necessary.
$t->{location} -= 1
    if $t->{location}
   and $t->{ltr}
   and $t->{ltr} eq "3p";

$t->{orientation_in_reference} = $r->{insert_orientation} =~ y/+-/FR/r;
$t->{orientation_in_gene}      = $r->{gene_orientation}   =~ y/+-/FR/r;

$t->{ncbi_gene_id} = $r->{gene_id};

# For debugging/cross-checking in a subsequent step?
$t->{gene} = $r->{inserted_gene}
    if $r->{inserted_gene}
   and $r->{inserted_gene} ne "not found";

$t->{note} = $r->{comment};

# Normalizing empty strings to undef/NULL should be handled by the loading
# step.

push_output($t)
    for 1 .. ($r->{count} || 1);
