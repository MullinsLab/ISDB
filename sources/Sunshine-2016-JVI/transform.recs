# This is a recs <https://recs.pl> snippet for use with `recs xform`.
# vim: set ft=perl :
use strict;
use warnings;
use 5.014;
use Clone qw< clone >;

my $t = {
    source_name => "Sunshine-2016-JVI",
    environment => "in vitro",
    sample => {
        pubmed_id => 26912621,
        # See also below for type-specific info.
    },
    info => {
        source_file => $r->{file},
        replicates  => scalar @{ $r->{reads} },
    },
};


# Sample type specific info.  Static key-value pairs and named pattern captures
# are merged into the sample data structure.
my $sample_types = {
    qr/ACH2-clone(?<clone>\d+)/ => {
        tissue      => 'ACH-2 clone',
        tissue_url  => 'https://www.aidsreagent.org/reagentdetail.cfm?t=cell_lines&id=222',
    },
    qr/BCL/ => {
        tissue      => 'Primary CD4+ T cells (Bcl-2 transduced)',
        tissue_url  => 'https://www.ncbi.nlm.nih.gov/pmc/articles/PMC4378543/',
    },
    qr/CD4/ => {
        tissue      => 'Primary central memory CD4+ T cells',
        tissue_url  => 'https://www.ncbi.nlm.nih.gov/pmc/articles/PMC2614643/',
    },
};

for my $pattern (sort keys %$sample_types) {
    next unless $r->{file} =~ $pattern;
    $t->{sample} = {
        %{ $t->{sample} },              # All samples
        %{ $sample_types->{$pattern} }, # This sample type
        %+,                             # Named captures from the filename
    };
    last;
}


# No need to adjust position given how data is generated by orientation.py in li_hiv.git
$t->{landmark} = $r->{chrom};
$t->{location} = $r->{pos};
$t->{ltr}      = { "5prime" => "5p", "3prime" => "3p" }->{ $r->{ltr} };


# We don't know which unplaced scaffold it is, so we can't use a RefSeq
# accession for "chrUn" data.  Luckily, the number of affected rows is in the
# single digits.
delete $t->{landmark}
    if $t->{landmark} =~ /^chrUn/;

delete $t->{location}
    if not $t->{landmark};

$t->{orientation_in_landmark} = $r->{orientation};


# Convert to zero-origin, interbase coordinates assuming that the reported
# location is base-numbered and indicates the first chromosomal base outside of
# the indicated HIV LTR.  If the HIV LTR is not indicated, assume 5' and hence
# no conversion necessary.
$t->{location} -= 1
    if $t->{location}
   and $t->{ltr}
   and (   ($t->{ltr} eq "3p" and $t->{orientation_in_landmark} eq "F")
        or ($t->{ltr} eq "5p" and $t->{orientation_in_landmark} eq "R"));


# Spread grouped reads out into multiple output replicates.  Reads were already
# dedupped in our source data, so we're relatively confident we're working with
# independent evidence in each read.
my $replicate = 1;

for my $read (@{ $r->{reads} }) {
    my $tprime = clone($t);
    $tprime->{info}{replicate}   = $replicate++;
    $tprime->{info}{source_line} = $read->{line};
    push_output($tprime);
}
