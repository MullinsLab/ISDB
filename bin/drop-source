#!/usr/bin/env perl
use 5.010;
use strict;
use warnings;
use open qw< :std :encoding(UTF-8) >;
use FindBin qw< $Bin $Script >;
use lib "$Bin/../lib";
use ISDB::Schema;
use JSON::MaybeXS;
use Path::Tiny;

$|++;

my $source_metadata = shift
    or die "usage: $Script source-metadata.json\n";

$source_metadata = path($source_metadata);

die "No such file $source_metadata\n"
    unless $source_metadata->is_file;

my $source = (decode_json($source_metadata->slurp_raw) || {})->{name}
    or die "Cannot read source name from $source_metadata.  Is it JSON?\n";

print "Dropping all data from source $source... ";

my $db  = ISDB::Schema->connect_default;
my $txn = $db->txn_scope_guard;

if ($source = $db->resultset("Source")->find({ source_name => $source })) {
    $source->integrations->delete;
    $source->delete;
}

$txn->commit;

say "OK", !$source ? " # doesn't exist" : "";
