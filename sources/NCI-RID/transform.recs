# This is a recs <https://recs.pl> snippet for use with `recs xform`.
# vim: set ft=perl :
use strict;
use warnings;
use 5.014;
our $line;  # from recs

my $t = {
    source_name => "NCI-RID",
    sample => {
        original_id => $r->{origin_id},
        pubmed_id   => $r->{pubmed_id},
        integration_environment => "in vivo", # default, overwritten by some sources below
    },
    info => {
        source_line => $line + 1,   # header isn't counted
    },
};

# Named captures get copied into the sample document.
my %sample_extractor = (
    24968937 => qr/^(?<subject>PT[1-5])(?:_Y(?<years_on_art>[\d.]+))?/i,    # Maldarelli, et al.
    12843741 => qr/^(?<clone>(?:AL|AD|C)-\d+)_/,                            # Mack, et al., and also handled specially below
    15163705 => qr/^(?<subject>\d+)/,                                       # Han, et al.
    17262715 => qr/^(?<genbank>.+?)(?:\.\d+)?$/,                            # Ikeda, et al.
  # 23953889, Sherrill-Mix, et al, handled specially below
);
if (my $re = $sample_extractor{ $t->{sample}{pubmed_id} }) {
    if ($t->{sample}{original_id} =~ $re) {
        $t->{sample}{$_} = $+{$_}
            for keys %+;
    }
}

# 23953889, Sherrill-Mix, et al.
if ($t->{sample}{pubmed_id} == 23953889) {
    $t->{sample}{tissue} = "cell culture";
    $t->{sample}{integration_environment} = "in vitro";
}

# 12843741, Mack, et al.
if ($t->{sample}{pubmed_id} == 12843741 and $t->{sample}{clone}) {
    my %clones;

    # Subjects
    $clones{$_}{subject} = "Case #1" for map { "AL-$_" }  1 .. 12;
    $clones{$_}{subject} = "Case #2" for map { "AL-$_" } 13 .. 16;
    $clones{$_}{subject} = "Case #3" for map { "AL-$_" } 17 .. 19;
    $clones{$_}{subject} = "Case #4" for map { "AD-$_" }  1 .. 15;

    # Tissues
    $clones{$_}{tissue} = "lymph node"     for map { "AL-$_" }  1 .. 4, 13 .. 19;
    $clones{$_}{tissue} = "spleen"         for map { "AL-$_" }  5 .. 12;
    $clones{$_}{tissue} = "frontal cortex" for map { "AD-$_" }  1 .. 10;
    $clones{$_}{tissue} = "meninges"       for map { "AD-$_" } 11 .. 13;
    $clones{"AD-14"}{tissue} = "subcortex";
    $clones{"AD-15"}{tissue} = "macrophage";

    for (map { "C-$_" } 1 .. 3) {
        $clones{$_}{tissue}     = "OM-10 cell line";
        $clones{$_}{tissue_url} = "https://www.aidsreagent.org/pdfs/ds1319_011.pdf";
        $clones{$_}{integration_environment} = "in vitro";
    }

    # Disease
    $clones{$_}{disease} = "angioimmunoblastic lymphadenopathy (AILD)"
        for map { "AL-$_" }  1 .. 12;
    $clones{$_}{disease} = "lymphoma"
        for map { "AL-$_" } 13 .. 19;
    $clones{$_}{disease} = "AIDS dementia"
        for map { "AD-$_" }  1 .. 15;

    # Copy properties on our IS record
    if (my $clone = $clones{ $t->{sample}{clone} }) {
        $t->{sample}{$_} = $clone->{$_}
            for keys %$clone;
    }
}

$t->{ltr} = { "5LTR" => "5p", "3LTR" => "3p" }->{ $r->{LTR} }
    if $r->{LTR};

$t->{landmark} = uc($r->{chr}) =~ s/^CHR/chr/ir;
$t->{location} = $r->{insert_position};

# We don't know which unplaced scaffold it is, so we can't use a RefSeq
# accession for "chrUn" data.  Luckily, the number of affected rows is in the
# single digits.
delete $t->{landmark}
    if $t->{landmark} eq "chrUN";

delete $t->{location}
    if not $t->{landmark};

$t->{orientation_in_landmark} = $r->{insert_orientation} =~ y/+-/FR/r;

# Convert to zero-origin, interbase coordinates assuming that the reported
# location is base-numbered and indicates the first chromosomal base outside of
# the indicated HIV LTR.  If the HIV LTR is not indicated, assume 5' and hence
# no conversion necessary.
$t->{location} -= 1
    if $t->{location}
   and $t->{ltr}
   and (   ($t->{ltr} eq "3p" and $t->{orientation_in_landmark} eq "F")
        or ($t->{ltr} eq "5p" and $t->{orientation_in_landmark} eq "R"));

$t->{note} = $r->{comment};

# Normalizing empty strings to undef/NULL should be handled by the loading
# step.

push_output($t)
    for 1 .. ($r->{count} || 1);
