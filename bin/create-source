#!/usr/bin/env perl
use 5.010;
use strict;
use warnings;
use open qw< :std :encoding(UTF-8) >;
use ISDB::Schema;
use JSON::PP;
use Types::Standard qw< :types slurpy >;
use Types::Common::String qw< :types >;

# Maybe someday we'll want to use a JSON schema (http://json-schema.org/) via
# JSON::Schema (or otherwise), but this is quick and easy and low-dep and still
# gets us nice validation.
# -trs, 2 Dec 2015
my $SourceMetadata = Dict[
    name    => NonEmptySimpleStr,
    uri     => NonEmptySimpleStr,
    slurpy HashRef
];

my $json     = do { local $/; <> };
my $metadata = decode_json($json);
$SourceMetadata->assert_valid($metadata);

print "Creating source $metadata->{name}... ";

my $db  = ISDB::Schema->connect_default;
my $txn = $db->txn_scope_guard;
$db->resultset("Source")->create({
    source_name => $metadata->{name},
    document    => $json,
});
$txn->commit;

say "OK";
