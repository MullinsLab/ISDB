#!/usr/bin/env perl
use strict;
use warnings;
use 5.010;

use JSON::MaybeXS;
use Getopt::Long;

my $DEBUG = 0;
GetOptions("debug|d" => \$DEBUG);

my @expecting;
my @clump;

while (<STDIN>) {
    my $r = decode_json($_);
    my @nearby_loc = split /; /, $r->{nearby_loc};

    if (@expecting) {
        # Check if our clump has changed
        if (not contained($r->{location}, @expecting)
            and not grep { contained($_, @expecting) } @nearby_loc)
        {
            output_best(@clump);

            # Clean slate for next clump
            @clump = ();
            @expecting = ();
        }
    }

    push @clump, $r;
    push @expecting, $r->{location}, @nearby_loc;
}
output_best(@clump);

# This is an embarrassingly-named function since it does the exact opposite.
# It outputs the _duplicates_, omitting the best record.  The goal is a list of
# records to delete, so it identifies the best and then prints everything else.
# -trs, 5 Jan 2017
sub output_best {
    return unless @_;

    # Process clump and identify location with highest multiplicity or
    # lowest location, if multiplicity is the same.
    my @ranked = sort {
        $b->{multiplicity} <=> $a->{multiplicity} ||
            $a->{location} <=> $b->{location}
    } @_;

    if ($DEBUG) {
        printf STDERR "%s %-5s %12d %3s %s %3d\n",
            @$_{qw[ subject landmark location ltr orient multiplicity ]}
                for @ranked;
        say STDERR "---";
    }

    die "Clump should always have both LTRs represented"
        unless grep { $_->{ltr} eq "5p" } @ranked
           and grep { $_->{ltr} eq "3p" } @ranked;

    say encode_json( $ranked[$_] )
        for 1 .. $#ranked;
}

sub contained {
    my $item = shift;
    return scalar grep { $_ == $item } @_;
}
